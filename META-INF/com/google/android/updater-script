set_progress(0.0);
ui_print(" <>>  <<>");
ui_print("< NanoMod 5.0.20170405 >");
ui_print(" <<>  <>>");
set_progress(0.1);

##############################
#
# Preparations
#
##############################

ui_print("> Mounting Partitions");
run_program("/sbin/mount", "/system");
run_program("/sbin/mount", "/data");

package_extract_file("nano.sh", "/tmp/nano.sh");
set_metadata("/tmp/nano.sh", "uid", 0, "gid", 0, "mode", 0777);
run_program("/tmp/nano.sh", "mkdir");

package_extract_file(".nanomod-overlay", "/tmp/.nanomod-overlay");
package_extract_file(".nanomod-setup", "/tmp/.nanomod-setup");

run_program("/tmp/nano.sh", "overlay");
run_program("/tmp/nano.sh", "setup");

package_extract_file("unsu.sh", "/tmp/unsu.sh");
set_metadata("/tmp/unsu.sh", "uid", 0, "gid", 0, "mode", 0777);

##############################
#
# Install User Applications
#
##############################

set_progress(0.2);
ui_print("> Installing User Apps");

### work-around for bug in Magisk [155] ###

ui_print(" << DroidGuard Helper");
package_extract_dir("org.microg.gms.droidguard", "/data/app/org.microg.gms.droidguard");

##############################
#
# Permissions
#
##############################

set_progress(0.4);
ui_print("> Setting permissions");
run_program("/tmp/nano.sh", "dataperms");

##############################
#
# Magisk
#
##############################

set_progress(0.6);

if file_getprop("/data/media/0/.nanomod-setup", "nanomod.unsu") == "1" then 
	ui_print("> Removing existing SuperUser");
	run_program("/tmp/unsu.sh");
endif;

if file_getprop("/data/media/0/.nanomod-setup", "nanomod.magisk") == "1" then 
	ui_print("> Installing Magisk");
	package_extract_file("Magisk.zip", "/tmp/magisk/magisk.zip");
	run_program("/sbin/unzip", "/tmp/magisk/magisk.zip", "META-INF/com/google/android/*", "-d", "/tmp/magisk");
	run_program("/sbin/sh", "/tmp/magisk/META-INF/com/google/android/update-binary", "dummy", "1", "/tmp/magisk/magisk.zip");
endif;

##############################
#
# Overlay
#
##############################

set_progress(0.8);
ui_print("> Installing Overlay");

if file_getprop("/data/media/0/.nanomod-setup", "nanomod.reinstall") == "1" then 
	ui_print("> Back-Up patched services.jar [as requested]");
	run_program("/tmp/nano.sh", "backup-servicesjar");
endif;

package_extract_file("Overlay.zip", "/tmp/overlay/overlay.zip");
run_program("/sbin/unzip", "/tmp/overlay/overlay.zip", "META-INF/com/google/android/*", "-d", "/tmp/overlay");
run_program("/sbin/sh", "/tmp/overlay/META-INF/com/google/android/update-binary", "dummy", "1", "/tmp/overlay/overlay.zip");

if file_getprop("/data/media/0/.nanomod-setup", "nanomod.reinstall") == "1" then 
	ui_print("> Restore patched services.jar [as requested]");
	run_program("/tmp/nano.sh", "restore-servicesjar");
endif;

##############################
#
# Clean-Up
#
##############################

ui_print("> Clean-Up");
run_program("/tmp/nano.sh", "rmdir");

ui_print("> Unmounting Partitions");
run_program("/sbin/umount", "/system");
run_program("/sbin/umount", "/data");

ui_print(" <>>  <<>");
ui_print("< Installation is completed! >");
ui_print("< Thanks for using NanoMod >");
ui_print(" <<>  <>>");

set_progress(1.0);
